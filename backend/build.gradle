plugins {
	id 'org.springframework.boot' version '2.7.1'
	id 'io.spring.dependency-management' version '1.0.11.RELEASE'
	id 'org.asciidoctor.jvm.convert' version '3.3.2'
	id "org.sonarqube" version "3.0"
	id 'jacoco'
	id 'java'
}

group = 'com.woowacourse'
version = '0.0.1-SNAPSHOT'
sourceCompatibility = '11'

configurations {
	compileOnly {
		extendsFrom annotationProcessor
	}
	asciidoctorExt
}

repositories {
	mavenCentral()
}

dependencies {
	// spring
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-validation'

	// lombok
	compileOnly 'org.projectlombok:lombok'
	annotationProcessor 'org.projectlombok:lombok'

	// db
	runtimeOnly 'com.h2database:h2:2.1.214'
	runtimeOnly 'org.mariadb.jdbc:mariadb-java-client'

	// test
	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'io.rest-assured:rest-assured:4.4.0'

	// rest docs
	testImplementation'org.springframework.restdocs:spring-restdocs-restassured'
	testImplementation'org.springframework.restdocs:spring-restdocs-mockmvc'
	asciidoctorExt 'org.springframework.restdocs:spring-restdocs-asciidoctor'

	// jwt
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-impl:0.11.5'
	runtimeOnly 'io.jsonwebtoken:jjwt-jackson:0.11.5'

	// BCrypt
	implementation 'org.mindrot:jbcrypt:0.4'

	// handlebars
	implementation 'pl.allegro.tech.boot:handlebars-spring-boot-starter:0.3.4'
}

ext {
	snippetsDir = file('build/generated-snippets')
}

test {
	outputs.dir snippetsDir
	useJUnitPlatform()
}

asciidoctor {
	inputs.dir snippetsDir
	configurations 'asciidoctorExt'
	dependsOn test
}

task createDocument(type: Copy) {
	dependsOn asciidoctor
	from "${asciidoctor.outputDir}"
	include '*.html'
	into 'src/main/resources/static/docs'
}

bootJar {
	dependsOn createDocument
	from("${asciidoctor.outputDir}") {
		into 'static/docs'
	}
}

jacoco {
	toolVersion('0.8.7')
}

jacocoTestReport {
	reports {
		html.enabled true
		csv.enabled false
		xml.enabled true
	}

	afterEvaluate {
		classDirectories.setFrom(
				files(classDirectories.files.collect {
					fileTree(dir: it,
							excludes: ['**/support/**', '**/dto/**', '**/*Application*', '**/*BaseEntity*',
									   '**/*Github*', '**/aspect/**', '**/*Config*', '**/AdminAuthInterceptor*', '**/AdminController*'])
				})
		)
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			enabled = true
			element = 'CLASS'

			limit {
				counter = 'LINE'
				value = 'COVEREDRATIO'
				minimum = 0.80
			}

			limit {
				counter = 'BRANCH'
				value = 'COVEREDRATIO'
				minimum = 0.80
			}

			excludes = ['*support*', '*dto*', '*Application*', '*BaseEntity*', '*Github*', '*aspect*', '*Config*', '*AdminAuthInterceptor*', '*AdminController*']
		}
	}
}

task testCoverage {
	group 'verification'
	description 'Run tests with coverage'

	dependsOn('test', 'jacocoTestReport', 'jacocoTestCoverageVerification')

	tasks['jacocoTestReport'].mustRunAfter(tasks['test'])
	tasks['jacocoTestCoverageVerification'].mustRunAfter(tasks['jacocoTestReport'])
}

sonarqube {
	properties {
		property "sonar.test.inclusions", "**/*Test.java"
		property 'sonar.coverage.jacoco.xmlReportPaths', '${buildDir}/reports/jacoco/test/jacocoTestReport.xml'
		property 'sonar.exclusions',
				'**/support/**, **/dto/**, **/*Application*, **/*BaseEntity*, **/*Github*, **/aspect/**, **/*Config*, **/*AdminAuthInterceptor*, **/*AdminController*'

		property 'sonar.issue.ignore.multicriteria', 'e1, e2'
		property 'sonar.issue.ignore.multicriteria.e1.ruleKey', 'java:S6212'
		property 'sonar.issue.ignore.multicriteria.e1.resourceKey', '**/*.java'
		property 'sonar.issue.ignore.multicriteria.e2.ruleKey', 'java:S2699'
		property 'sonar.issue.ignore.multicriteria.e2.resourceKey', '**/*ControllerTest.java'
	}
}
